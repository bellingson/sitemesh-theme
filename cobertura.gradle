
// add -Pversion=xxx to command line to add version

def cobSerFile = "${project.buildDir}/cobertura.ser"
def srcOriginal = "${sourceSets.main.classesDir}"
def srcCopy = "${srcOriginal}-copy"
def reportDir = "${project.buildDirName}/reports/coverage"

dependencies {
     compile group: 'net.sourceforge.cobertura', name: 'cobertura', version: '1.9.3'
}

test.doFirst  {
    ant {
        // delete data file for cobertura, otherwise coverage would be added
        delete(file:cobSerFile, failonerror:false)
        // delete copy of original classes
        delete(dir: srcCopy, failonerror:false)
        // import cobertura task, so it is available in the script
        taskdef(resource:'tasks.properties', classpath: configurations.testRuntime.asPath)
        // create copy (backup) of original class files
        copy(todir: srcCopy) {
            fileset(dir: srcOriginal)
        }
        // instrument the relevant classes in-place
        'cobertura-instrument'(datafile:cobSerFile) {
            fileset(dir: srcOriginal,
                   includes:"nfjs/**/*.class",
                   excludes:"**/*Test.class")
        }
    }
}

test {
    // pass information on cobertura datafile to your testing framework
    // see information below this code snippet

    systemProperties["net.sourceforge.cobertura.datafile"] = cobSerFile

}

test.doLast {
    if (new File(srcCopy).exists()) {
        // replace instrumented classes with backup copy again
        ant {
            delete(file: srcOriginal)
            move(file: srcCopy,
                     tofile: srcOriginal)
        }
        // create cobertura reports
        ant.'cobertura-report'(destdir:reportDir,
             format:'html', srcdir:"src/main/java", datafile:cobSerFile)
    }
}